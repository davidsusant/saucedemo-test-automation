plugins {
    id 'java'
    id 'io.qameta.allure' version '2.11.2'
}

group 'com.saucedemo.test.automation'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

ext {
    seleniumVersion = '4.38.0'
    allureVersion = '2.24.0'
    aspectJVersion = '1.9.20'
}

configurations {
    agent
}

dependencies {
    implementation "org.seleniumhq.selenium:selenium-java:${seleniumVersion}"
    implementation 'io.cucumber:cucumber-java:7.14.0'
    implementation 'io.cucumber:cucumber-testng:7.14.0'
    implementation 'org.testng:testng:7.8.0'
    implementation 'io.github.bonigarcia:webdrivermanager:5.6.2'

    implementation("ch.qos.logback:logback-classic:1.5.20")

    // Allure dependencies
    implementation "io.qameta.allure:allure-testng:${allureVersion}"
    implementation "io.qameta.allure:allure-cucumber7-jvm:${allureVersion}"
    implementation "io.qameta.allure:allure-attachments:${allureVersion}"
    agent "org.aspectj:aspectjweaver:${aspectJVersion}"
}

allure {
    version = allureVersion
    adapter {
        frameworks {
            cucumber {
                adapterVersion = allureVersion
            }
        }
    }
}

test {
    useTestNG() {
        suites 'src/test/resources/testng.xml'
    }

    testLogging {
        events "passed", "skipped", "failed", "standardOut", "StandardError"
        showStandardStreams = true
    }

    systemProperty 'allure.results.directory', 'allure-results'

    // AspectJ Weaver for Allure
    doFirst {
        jvmArgs "-javaagent:${configurations.agent.singleFile}"
    }

    systemProperty 'cucumber.filter.tags', System.getProperty('cucumber.filter.tags', '')

    // Clean allure results and report before running tests
    doFirst {
        delete file('allure-results')
        delete file('allure-report')
    }
}

task runTests(type: Test) {
    useTestNG() {
        suites 'src/test/resources/testng.xml'
    }

    doFirst {
        jvmArgs "-javaagent:${configurations.agent.singleFile}"
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}